def packageName = "zerolog-benchmarks"
def packageDescription = "Benchmarks"

dependencies {
  api project(":")

  testImplementation "com.obsidiandynamics.fulcrum:fulcrum-dyno:${fulcrumVersion}"
  testImplementation "com.obsidiandynamics.fulcrum:fulcrum-testmark:${fulcrumVersion}"
  testImplementation "org.slf4j:slf4j-api:${slf4jVersion}"
  testImplementation "log4j:log4j:${log4jVersion}"
  testImplementation "org.tinylog:tinylog:${tinylogVersion}"
  testImplementation "org.openjdk.jmh:jmh-core:${jmhVersion}"

  testImplementation "com.obsidiandynamics.fulcrum:fulcrum-launcher:${fulcrumVersion}"
  testImplementation "org.slf4j:slf4j-log4j12:${slf4jVersion}"
}

task launch(dependsOn: build) {
  doLast {
    def launcherClasses = new ArrayList()
    launcherClasses += "com.obsidiandynamics.zerolog.bench.AllBenchmarks"
    launcherClasses += "com.obsidiandynamics.zerolog.bench.BaselineBenchmark"
    launcherClasses += "com.obsidiandynamics.zerolog.bench.JulBenchmark"
    launcherClasses += "com.obsidiandynamics.zerolog.bench.JulLambdaBenchmark"
    launcherClasses += "com.obsidiandynamics.zerolog.bench.Log4jBenchmark"
    launcherClasses += "com.obsidiandynamics.zerolog.bench.Slf4jBenchmark"
    launcherClasses += "com.obsidiandynamics.zerolog.bench.TinyLogBenchmark"
    launcherClasses += "com.obsidiandynamics.zerolog.bench.ZlgBenchmark"
    
    def perfJvmArgs = "-XX:-MaxFDLimit -XX:+TieredCompilation -XX:+UseNUMA -XX:+UseCondCardMark " + 
                      "-Xms2G -Xmx2G -Xss1M -XX:+UseG1GC -XX:MaxGCPauseMillis=200 " +
                      "-XX:InitiatingHeapOccupancyPercent=0 -Djava.net.preferIPv4Stack=true " + 
                      "-Dlauncher.package.compress.level=3"
    javaexec {
      systemProperties = System.properties
      classpath = sourceSets.main.compileClasspath + sourceSets.main.runtimeClasspath + sourceSets.test.compileClasspath + sourceSets.test.runtimeClasspath
      mainClass = "com.obsidiandynamics.launcher.Launcher"
      args = launcherClasses
      jvmArgs = Arrays.asList(perfJvmArgs.split(" ")) + baseJvmArgs
      standardInput = System.in
    }
  }
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      artifactId = packageName
      from components.java
      pom {
        name = packageName
        description = packageDescription
        url = 'https://github.com/obsidiandynamics/zerolog'
        licenses {
          license {
            name = 'BSD 3-Clause License'
            url = 'https://opensource.org/licenses/BSD-3-Clause'
          }
        }
        developers {
          developer {
            id = 'ekoutanov'
            name = 'Emil Koutanov'
            email = 'ekoutanov@gmail.com'
          }
        }
        scm {
          connection = 'scm:git:git://github.com/obsidiandynamics/zerolog.git'
          developerConnection = 'scm:git:ssh://github.com/obsidiandynamics/zerolog.git'
          url = 'https://github.com/obsidiandynamics/zerolog'
        }
      }
    }
  }
}